<languageVersion: 1.0;>
  
kernel HSVmap
<	vendor : "MIT Media Laboratory";
	namespace : "filters";
	version : 1;
	description : "Scratch HSVmap - maps hue, saturation, or brightness to levels of gray";
>
{
	parameter int mode
	<
		minValue: 0;
		maxValue: 3;
		defaultValue: 0;
	>;
	input image4 src;
	output float4 dst;

	void evaluatePixel() {
		float4 pix = sampleNearest(src, outCoord());
		if ((pix.a == 0.0) || (mode < 1) || (mode > 3)) {
			dst = pix;
		} else {
			float r = pix.r;
			float g = pix.g;
			float b = pix.b;
			// compute h, s, v
			float h, s;
			float v = max(r, max(g, b));
			float span = v - min(r, min(g, b));
			if (span == 0.0) {
				h = s = 0.0; // grayscale
			} else {
			if (r == v) h = 60.0 * ((g - b) / span);
				else if (g == v) h = 120.0 + (60.0 * ((b - r) / span));
				else if (b == v) h = 240.0 + (60.0 * ((r - g) / span));
				s = span / v;
			}

			float n;
			if (mode == 1) n = h / 360.0;
			if (mode == 2) n = 1.0 - s; // more saturated is darker
			if (mode == 3) n = v;
			dst.argb = float4(1.0, n, n, n);
		}
	}
}
